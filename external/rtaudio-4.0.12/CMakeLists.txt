SET(LIBRARY_DEPENDENCIES)
SET(HEADER_DEPENDENCIES)
SET(AUDIO_SYSTEMS)
SET(ASIO_SOURCES)

IF(WIN32)
  SET(AUDIO_SYSTEMS ${AUDIO_SYSTEMS} __WINDOWS_DS__)
  SET(LIBRARY_DEPENDENCIES ${LIBRARY_DEPENDENCIES} ${CMAKE_CURRENT_SOURCE_DIR}/lib/dsound.lib)

  SET(AUDIO_SYSTEMS ${AUDIO_SYSTEMS} __WINDOWS_ASIO__)
  SET(ASIO_SOURCES include/asiodrivers.cpp include/asiolist.cpp include/asio.cpp)
ELSEIF(APPLE)
  SET(AUDIO_SYSTEMS ${AUDIO_SYSTEMS} __MACOSX_CORE__)
  FIND_LIBRARY(COREAUDIO_LIBRARY CoreAudio)
  FIND_LIBRARY(COREFOUNDATION_LIBRARY CoreFoundation)
  SET(LIBRARY_DEPENDENCIES ${LIBRARY_DEPENDENCIES} ${COREAUDIO_LIBRARY} ${COREFOUNDATION_LIBRARY} pthread)
ELSEIF(UNIX)
  FIND_PACKAGE(ALSA)
  IF(ALSA_FOUND)
    SET(AUDIO_SYSTEMS ${AUDIO_SYSTEMS} __LINUX_ALSA__)
    SET(HEADER_DEPENDENCIES ${HEADER_DEPENDENCIES} ${ALSA_INCLUDE_DIRS})
    SET(LIBRARY_DEPENDENCIES ${LIBRARY_DEPENDENCIES} ${ALSA_LIBRARIES})
  ENDIF()

  FIND_PACKAGE(Jack)
  IF(JACK_FOUND)
    SET(AUDIO_SYSTEMS ${AUDIO_SYSTEMS} __UNIX_JACK__)
    SET(HEADER_DEPENDENCIES ${HEADER_DEPENDENCIES} ${JACK_INCLUDE_DIRS})
    SET(LIBRARY_DEPENDENCIES ${LIBRARY_DEPENDENCIES} ${JACK_LIBRARIES})
  ENDIF()

  # Disable OSS since supporting it is cumbersome.
  # It is also nominated for removal from RtAudio.
  #FIND_PACKAGE(OSS)
  IF(OSS_FOUND)
    SET(AUDIO_SYSTEMS ${AUDIO_SYSTEMS} __LINUX_OSS__)
    SET(HEADER_DEPENDENCIES ${HEADER_DEPENDENCIES} ${OSS_INCLUDE_DIR})
  ENDIF()

  SET(LIBRARY_DEPENDENCIES ${LIBRARY_DEPENDENCIES} pthread)
ELSE()
  SET(AUDIO_SYSTEMS ${AUDIO_SYSTEMS} __RTAUDIO_DUMMY__)
ENDIF()

_MESSAGE("Configured audio system(s): " ${AUDIO_SYSTEMS})

FOREACH(AUDIO_SYSTEM ${AUDIO_SYSTEMS})
  ADD_DEFINITIONS(-D${AUDIO_SYSTEM})
ENDFOREACH()

INCLUDE_DIRECTORIES(${HEADER_DEPENDENCIES})

ADD_LIBRARY(rtaudio ${LIBRARY_TYPE} RtAudio.cpp ${ASIO_SOURCES})
TARGET_LINK_LIBRARIES(rtaudio ${LIBRARY_DEPENDENCIES})

SET(RTAUDIO_INCLUDE_DIRS ${CMAKE_CURRENT_SOURCE_DIR} CACHE INTERNAL "Include directories for RtAudio")
SET(RTAUDIO_LIBRARY rtaudio CACHE INTERNAL "RtAudio library")

_SUBDIRS(tests)
